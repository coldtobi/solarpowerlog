
dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/solarpowerlog.cpp)
AC_PREREQ(2.59)
# use own directory for the autotools stuff
AC_CONFIG_AUX_DIR(config)

CPPFLAGS="-Wall -Wextra"

# we want this header to be created. 
AC_CONFIG_HEADERS(src/config.h)

# TODO
#AC_COPYRIGHT (copyright-notice )

AM_INIT_AUTOMAKE(solarpowerlog, 0.01)

AC_CONFIG_SRCDIR([src/solarpowerlog.cpp])

## option for disable logging (default: on)
AC_ARG_ENABLE([logging],
	[AS_HELP_STRING([--disable-logging], [Disable logging facility. \
		Error output is limited to stdout/err and not configureable.])
	]
)

### option to disable some modules. Default is to compile everything. 

## Inverters
# Sputnik support.
AC_ARG_ENABLE([sputnik],
	[AS_HELP_STRING([--disable-sputnik],
			[Do not support inverters by Sputnik engineering.])
	]
)

if test "x$enable_sputnik" != "xno" ; then
	AC_DEFINE([HAVE_INV_SPUTNIK], [1], [Sputnik inverter support])	
	enable_sputnik=yes
else
	AC_MSG_NOTICE([Support for Sputnik inverters disabled as requested.])
fi

## Filters / Dumpers

# CSV Writer
AC_ARG_ENABLE([csvlogger],
	[AS_HELP_STRING([--disable-csvlogger],
			[Do not support logging to CSV files.])
	]
)

if test "x$enable_csvlogger" != "xno" ; then
	AC_DEFINE([HAVE_FILTER_CSVDUMP], [1], [CSV Writer Support])	
	enable_csvlogger=yes
else
	AC_MSG_NOTICE([Support for CSV Writer disabled as requested.])
fi

# Dumb Dumper
AC_ARG_ENABLE([dumbdumper],
	[AS_HELP_STRING([--disable-dumbdumper],
			[Do not support the "dumb dumper" logging filter.])
	]
)

if test "x$enable_dumbdumper" != "xno" ; then
	AC_DEFINE([HAVE_FILTER_DUMBDUMP], [1], [Dumb Dumper Support])	
	enable_dumbdumper=yes
else
	AC_MSG_NOTICE([Support for the Dumb Dumper disabled as requested.])
fi


### Checks for programs.
# C Compiler
AC_PROG_CC
# C++ Compiler
AC_PROG_CXX
# pkg-config
PKG_CHECK_EXISTS

# We only use C++...
AC_LANG(C++)

### Checks for libraries.

## Check for libconfig. Always required.
PKG_CHECK_MODULES(CONFIG, libconfig++ >= 1.3.1)

## Check for library liblog4cxx. Can be turned off.  
if test "x$enable_logging" != "xno" ; then
	PKG_CHECK_MODULES(LOG4CXX, liblog4cxx >= 0.10.0, [AC_DEFINE([HAVE_LIBLOG4CXX], [1], [Logging Library])], AC_MSG_RESULT([LibLog4CXX not found]))
else
	AC_MSG_NOTICE([Logging disabled as requested.])
	LOG4CXX_PKG_ERRORS="x"
fi

## Check for Boost 
AX_BOOST_BASE([1.38])

AX_BOOST_SYSTEM
if test "x$BOOST_SYSTEM_LIB" = "x"; then
   AC_MSG_ERROR([boost::system library $BOOST_SYSTEM_LIB not found ])
fi

AX_BOOST_THREAD
if test "x$BOOST_THREAD_LIB" = "x"; then
   AC_MSG_ERROR([boost::thread library not $BOOST_THREAD_LIB found ])
fi

AX_BOOST_DATE_TIME
if test "x$BOOST_DATE_TIME_LIB" = "x"; then
  AC_MSG_ERROR([boost::date_time library $BOOST_DATE_TIME_LIB not found ])
fi

AX_BOOST_PROGRAM_OPTIONS
if test "x$BOOST_PROGRAM_OPTIONS_LIB" = "x"; then
   AC_MSG_ERROR([boost::programm_options library $BOOST_PROGRAM_OPTIONS_LIB not found ])
   exit 1
fi

# BOOST ASIO (Header-only-library)
# note: we are requiring boost 1.38, so we do not need to check the asio version here.
AC_CHECK_HEADER([boost/asio/version.hpp], ,[AC_MSG_ERROR("boost::asio header not found")])


### Checks for header files.
AC_CHECK_HEADER([w32api/wtypes.h],
 [AC_DEFINE([HAVE_WIN32_API], [1], [Windows API]) ])

if test "x$ac_cv_header_w32api_wtypes_h" = "xyes"; then
  AC_MSG_RESULT([WIN32 API detected. ])
  WIN32_LIBS="-lws2_32"
  AC_SUBST(WIN32_LIBS)
fi


### Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_HEADER_TIME
AC_HEADER_ASSERT
AC_TYPE_SIZE_T
AC_CXX_EXPLICIT
AC_CXX_HAVE_STL


### Checks for library functions.
#AC_CHECK_FUNCS([socket])

AC_PROG_INSTALL

AC_CONFIG_FILES(
	[Makefile]
	[src/Makefile]
)
AC_OUTPUT

AC_MSG_NOTICE([]);
AC_MSG_NOTICE([CONFIGURATION SUMMARY:]);
### Print summary
if test "x$LOG4CXX_PKG_ERRORS" != "x" ; then
	AC_MSG_NOTICE([Logging support: ........................... no])
else
	AC_MSG_NOTICE([Logging support: ........................... yes])
fi

AC_MSG_NOTICE([INVERTERS:]);

if test "x$enable_sputnik" != "xyes" ; then
	AC_MSG_NOTICE([Sputnik support: ........................... no])
else
	AC_MSG_NOTICE([Sputnik support: ........................... yes])
fi

AC_MSG_NOTICE([FILTERS AND LOGGERS:]);
if test "x$enable_csvlogger" != "xyes" ; then
	AC_MSG_NOTICE([CSV logger support: ........................ no])
else
	AC_MSG_NOTICE([CSV logger support: ........................ yes])
fi

if test "x$enable_dumbdumper" != "xyes" ; then
	AC_MSG_NOTICE([Dumb Dumper support: ....................... no])
else
	AC_MSG_NOTICE([Dumb Dumper support: ....................... yes])
fi



